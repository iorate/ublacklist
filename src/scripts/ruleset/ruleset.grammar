@precedence {
  and @left,
  or @left
}

@skip { space | Comment }

@top Ruleset { Rule? ("\n" Rule?)* }

Rule { (NegateSpecifier | HighlightSpecifier)? ((MatchPattern IfSpecifier?) | expression) }

NegateSpecifier { "@" }
HighlightSpecifier { AtInteger }
IfSpecifier { "@if" "(" expression ")" }

@skip {} {
  MatchPattern {
      "<all_urls>"
    | ( "*"
      | @extend<
          Identifier,
            "HTTP" | "HTTp" | "HTtP" | "HTtp" | "HtTP" | "HtTp" | "HttP" | "Http" | "hTTP" | "hTTp" | "hTtP" | "hTtp" | "htTP" | "htTp" | "httP" | "http"
          | "HTTPS" | "HTTpS" | "HTtPS" | "HTtpS" | "HtTPS" | "HtTpS" | "HttPS" | "HttpS" | "hTTPS" | "hTTpS" | "hTtPS" | "hTtpS" | "htTPS" | "htTpS" | "httPS" | "httpS" | "HTTPs" | "HTTps" | "HTtPs" | "HTtps" | "HtTPs" | "HtTps" | "HttPs" | "Https" | "hTTPs" | "hTTps" | "hTtPs" | "hTtps" | "htTPs" | "htTps" | "httPs" | "https"
        >
      )
      "://"
      matchPatternHost
      matchPatternPath
  }
}

expression[@isGroup=Expression] {
    MatchExpression {
        Identifier StringMatchOperator String
      | (Identifier RegExpMatchOperator?)? RegExp
    }
  | ParenthesizedExpression { "(" expression ")" }
  | NegateExpression { "!" expression }
  | AndExpression { expression !and "&" expression }
  | OrExpression { expression !or "|" expression }
}

@tokens {
  space { $[ \t\r]+ }
  Comment { "#" ![\n]* }

  "@" "@if"
  AtInteger { "@" ("0" | $[1-9] @digit*) }
  @precedence { "@if" AtInteger "@" }

  matchPatternLabel { (@digit | @asciiLetter) ((@digit | @asciiLetter | "-")* (@digit | @asciiLetter))? }
  matchPatternHost { ("*" | matchPatternLabel) ("." matchPatternLabel)* }
  hex { @digit | $[A-Fa-f] }
  matchPatternPathChar { "*" | @digit | @asciiLetter | $[._~:/?[\]@!$&'()+,;=-] | "%" hex hex }
  matchPatternPath { "/" matchPatternPathChar* }

  Identifier { (@asciiLetter | $[_$]) (@digit | @asciiLetter | $[_$])* }

  StringMatchOperator { $[^$*]? "=" }
  RegExpMatchOperator { "=~" }

  stringEscape { "\\" ![\n] }
  stringContentSingle { !['\\\n] }
  stringContentDouble { !["\\\n] }
  String { "'" (stringEscape | stringContentSingle)* "'" | '"' (stringEscape | stringContentDouble)* '"' }

  regExpEscape { "\\" ![\n] }
  regExpClassContent { ![\]\\\n] }
  regExpContent { ![/\\[\n] }
  regExpPattern { (regExpEscape | "[" regExpClassContent* "]" | regExpContent)+ }
  regExpFlags { $[imsu]+ }
  RegExp { "/" regExpPattern "/" regExpFlags? }

  "(" ")" "!" "&" "|"
}
